# day 16

## Section

```elixir
defmodule Packet do
  use Bitwise

  def decode!(bin) do
    {res, _} = decode(bin)
    res
  end

  def decode(<<version::3, rest::bits>>) do
    {payload, rest} = decode_type(rest)
    {%{version: version, payload: payload}, rest}
  end

  def decode_type(<<4::3, rest::bits>>) do
    {decoded, rest} = decode_literal(rest, 0)
    {%{type: :literal, value: decoded}, rest}
  end

  def decode_type(<<op::3, rest::bits>>) do
    {decoded, rest} = decode_operator(rest)

    op =
      case op do
        0 -> :sum
        1 -> :product
        2 -> :minimum
        3 -> :maximum
        5 -> :gt
        6 -> :lt
        7 -> :eq
      end

    {%{type: :op, op: op, value: decoded}, rest}
  end

  def decode_literal(<<1::1, value::4, rest::bits>>, acc) do
    decode_literal(rest, (acc <<< 4) + value)
  end

  def decode_literal(<<0::1, value::4, rest::bits>>, acc) do
    {(acc <<< 4) + value, rest}
  end

  def decode_operator(<<0::1, len::15, subpackets::bits-size(len), rest::bits>>) do
    {decode_len(subpackets), rest}
  end

  def decode_operator(<<1::1, count::11, rest::bits>>) do
    decode_count(count, rest)
  end

  defp decode_count(0, rest), do: {[], rest}

  defp decode_count(count, rest) do
    {decoded, rest} = decode(rest)
    {final, rest} = decode_count(count - 1, rest)
    {[decoded | final], rest}
  end

  defp decode_len(<<>>), do: []

  defp decode_len(<<rest::bits>>) do
    {decoded, rest} = decode(rest)
    [decoded | decode_len(rest)]
  end

  def fromHex(s), do: s |> Base.decode16!() |> Packet.decode!()

  def versions(%{version: version, payload: payload}) do
    [version | versions(payload)]
  end

  def versions(%{type: :literal}), do: []

  def versions(%{value: value}) do
    for v <- value, reduce: [] do
      acc -> acc ++ versions(v)
    end
  end

  def evaluate(%{type: :literal, value: value}), do: value

  def evaluate(%{type: :op, op: op, value: values} = operator) do
    IO.inspect(operator)

    args =
      for v <- values, into: [] do
        evaluate(v)
      end

    IO.inspect(args)

    case op do
      :sum ->
        Enum.sum(args)

      :product ->
        Enum.reduce(args, &(&1 * &2))

      :minimum ->
        Enum.min(args)

      :maximum ->
        Enum.max(args)

      :gt ->
        [fst, snd] = args
        if fst > snd, do: 1, else: 0

      :lt ->
        [fst, snd] = args
        if fst < snd, do: 1, else: 0

      :eq ->
        [fst, snd] = args
        if fst == snd, do: 1, else: 0
    end
  end

  def evaluate(%{payload: payload}) do
    evaluate(payload)
  end
end

"38006F45291200" |> Packet.fromHex() |> Packet.evaluate()
"EE00D40C823060" |> Packet.fromHex()
"8A004A801A8002F478" |> Packet.fromHex() |> Packet.evaluate()
```

```elixir
input =
  "E058F79802FA00A4C1C496E5C738D860094BDF5F3ED004277DD87BB36C8EA800BDC3891D4AFA212012B64FE21801AB80021712E3CC771006A3E47B8811E4C01900043A1D41686E200DC4B8DB06C001098411C22B30085B2D6B743A6277CF719B28C9EA11AEABB6D200C9E6C6F801F493C7FE13278FFC26467C869BC802839E489C19934D935C984B88460085002F931F7D978740668A8C0139279C00D40401E8D1082318002111CE0F460500BE462F3350CD20AF339A7BB4599DA7B755B9E6B6007D25E87F3D2977543F00016A2DCB029009193D6842A754015CCAF652D6609D2F1EE27B28200C0A4B1DFCC9AC0109F82C4FC17880485E00D4C0010F8D110E118803F0DA1845A932B82E200D41E94AD7977699FED38C0169DD53B986BEE7E00A49A2CE554A73D5A6ED2F64B4804419508B00584019877142180803715224C613009E795E58FA45EA7C04C012D004E7E3FE64C27E3FE64C24FA5D331CFB024E0064DEEB49D0CC401A2004363AC6C8344008641B8351B08010882917E3D1801D2C7CA0124AE32DD3DDE86CF52BBFAAC2420099AC01496269FD65FA583A5A9ECD781A20094CE10A73F5F4EB450200D326D270021A9F8A349F7F897E85A4020CF802F238AEAA8D22D1397BF27A97FD220898600C4926CBAFCD1180087738FD353ECB7FDE94A6FBCAA0C3794875708032D8A1A0084AE378B994AE378B9A8007CD370A6F36C17C9BFCAEF18A73B2028C0A004CBC7D695773FAF1006E52539D2CFD800D24B577E1398C259802D3D23AB00540010A8611260D0002130D23645D3004A6791F22D802931FA4E46B31FA4E4686004A8014805AE0801AC050C38010600580109EC03CC200DD40031F100B166005200898A00690061860072801CE007B001573B5493004248EA553E462EC401A64EE2F6C7E23740094C952AFF031401A95A7192475CACF5E3F988E29627600E724DBA14CBE710C2C4E72302C91D12B0063F2BBFFC6A586A763B89C4DC9A0"

input |> Packet.fromHex() |> Packet.versions() |> IO.inspect() |> Enum.sum()
```

```elixir
input |> Packet.fromHex() |> Packet.evaluate()
```
